# Name: 
# ID: 

# Execution begins at address 0 
	.pos 0
	irmovq  stack, %rsp  	# Set up stack pointer
	call    main		    # Execute main program
	halt			        # Terminate program 

# Sample linked list
.align 8
ele1:
    .quad   0x00a
    .quad   ele2
ele2:
    .quad   0x0b0
    .quad   ele3
ele3:
    .quad   0xc00
    .quad   0

main:
    irmovq  ele1, %rdi
    call    rsum_list       # rsum_list(ele1)
    ret

# long rsum_list(list_ptr ls)
# ls in %rdi
rsum_list:
    xorq    %rax, %rax      # res = 0
    andq    %rdi, %rdi      # Set CC
    je      recursion_stops_here_meow
    mrmovq  (%rdi), %r8     # val = ls->val
    pushq   %r8             # save val in stack 
    mrmovq  8(%rdi), %rdi   # ls = ls->next
    call    rsum_list       # rest = rsum_list(ls) in %rax 
    popq    %r8             # take val from stack 
    addq    %r8, %rax       # return val + rest
    
recursion_stops_here_meow:
    ret

# Stack starts here and grows to lower addresses
	.pos 0x200
stack:
